apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

ext.versions = [
        'supportLibrary': '27.1.1',
]

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "it.geosolutions.savemybike"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 12
        versionName "0.9.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        // Make sure this is consistent with the redirect URI used in res/raw/auth_config.json,
        // or specify additional redirect URIs in AndroidManifest.xml
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'it.geosolutions.savemybike'
        ]
    }
    buildTypes {
        debug {
            versionNameSuffix ".debug"
            buildConfigField "String", "PORTAL_ENDPOINT", '"https://dev.savemybike.geo-solutions.it/"'
            buildConfigField "String", "UPLOAD_RESOURCE", '"upload-dev/"'
            resValue "string", "app_version",
                    "${defaultConfig.versionName}${versionNameSuffix}"
        }
        beta {
            versionNameSuffix ".beta"
            buildConfigField "String", "PORTAL_ENDPOINT", '"https://goodgo.savemybike.geo-solutions.it/"'
            buildConfigField "String", "UPLOAD_RESOURCE", '"upload/"'
            minifyEnabled false
            resValue "string", "app_version",
                    "${defaultConfig.versionName}${versionNameSuffix}"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
        release {
            minifyEnabled false
            buildConfigField "String", "PORTAL_ENDPOINT", '"https://goodgo.savemybike.geo-solutions.it/"'
            buildConfigField "String", "UPLOAD_RESOURCE", '"upload/"'
            resValue "string", "app_version",
                "${defaultConfig.versionName}"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    android {
        lintOptions {
            abortOnError false
        }
    }
}

repositories {
    maven {
        url "https://dl.bintray.com/ghedeon/maven"
    }
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-v4:27.1.1'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-vector-drawable:27.1.1'
    implementation 'com.amazonaws:aws-android-sdk-s3:2.6.26'
    implementation 'com.amazonaws:aws-android-sdk-cognitoidentityprovider:2.6.26'
    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'net.openid:appauth:0.7.0'
    implementation 'joda-time:joda-time:2.10'

    implementation 'com.github.bumptech.glide:glide:4.7.1'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    testImplementation 'junit:junit:4.12'

    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    implementation 'com.chauthai.swipereveallayout:swipe-reveal-layout:1.4.1'
    implementation 'com.android.support:cardview-v7:27.0.0'
    implementation 'com.pacioianu.david:ink-page-indicator:1.3.0'
    implementation 'com.google.firebase:firebase-core:16.0.4'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            // Force all of the primary support libraries to use the same version.
            if (details.requested.group == 'com.android.support') {
                details.useVersion versions.supportLibrary
            }
        }
    }
}
